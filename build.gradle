plugins {
    id 'net.minecraftforge.gradle' version "${forgeGradleVersion}"
    id 'org.spongepowered.mixin' version "${mixingradle_version}"
    id 'eclipse'
    id 'idea'
    id 'org.parchmentmc.librarian.forgegradle' version "${parchmentGradleVersion}"
}

version = "$mcVersion-$modVersion"
group = 'de.dertoaster.crossbowverhaul'
archivesBaseName = "$modName"

java.toolchain.languageVersion = JavaLanguageVersion.of(17) // Mojang ships Java 17 to end users, so your mod should target Java 17.

minecraft {
    mappings channel: "${mappingsChannel}", version: "${mappingsVersion}"
    
    accessTransformer = 'src/main/resources/META-INF/accesstransformer.cfg'

    runs {
        client {
			taskName "runClient ${modName} ${mcVersion}"
            workingDirectory project.file('run')
			arg "-mixin.config=crossbowverhaul.mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                crossbowverhaul {
                    source sourceSets.main
                }
            }
        }

        server {
			taskName "runServer ${modName} ${mcVersion}"
            workingDirectory project.file('run')
            arg "-mixin.config=crossbowverhaul.mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                crossbowverhaul {
                    source sourceSets.main
                }
            }
        }

        data {
			taskName "runData ${modName} ${mcVersion}"
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'crossbowverhaul', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                crossbowverhaul {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft("net.minecraftforge:forge:${mcVersion}-${forgeVersion}")
	annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
}

mixin {
    add sourceSets.main, "crossbowverhaul.refmap.json"
	config "crossbowverhaul.mixins.json"
	
	debug.export = true
}

// Generate a fixed tsrg file after generating the default tsrg file
createMcpToSrg {
    outputs.upToDateWhen {false}
    doLast {
        fixFG5TsrgForMixinAP(output.get().asFile, file("${buildDir}/fixMcpToSrg/output.tsrg"))
    }
}

// Tell mixin to use the fixed TSRG file
mixin {
    reobfSrgFile = file("${buildDir}/fixMcpToSrg/output.tsrg")
}

// Function that actually fixes the TSRG file
static def fixFG5TsrgForMixinAP(File inFile, File outFile) {
    // Make directory if needed
    outFile.parentFile.mkdirs()

    try (Scanner scanner = new Scanner(inFile); PrintWriter out = new PrintWriter(outFile)) {
        boolean firstLine = true
        while (scanner.hasNextLine()) {
            String next = scanner.nextLine()

            // Skip first 'tsrg left right' header line
            if (firstLine) {
                firstLine = false
                continue
            }

            // Skip 'static' indicators
            if (next.trim() == "static") {
                continue
            }

            // Export line otherwise
            out.println(next)
        }
    }
}

jar.finalizedBy('reobfJar') 

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": "$modName",
            "Specification-Vendor": "DerToaster",
            "Specification-Version": "$modVersion", // We are version 1 of ourselves
            "Implementation-Title": "$modName",
            "Implementation-Version": "$modVersion",
            "Implementation-Vendor" :"DerToaster",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"MixinConfigs": "crossbowverhaul.mixins.json"
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar fil
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            artifact jar
//        }
//    }
//    repositories {
//        maven {
//            url "file:///${project.projectDir}/mcmodsrepo"
//        }
//    }
//}
